plugins {
    id 'com.android.library'
    id 'maven-publish' // Publishing code taken from https://stackoverflow.com/a/42160584
}

android {
    compileSdkVersion 31
    namespace 'com.evgenii.jsevaluator'

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 1
        versionName "cc8c7fe371"
        version="cc8c7fe371"
        archivesBaseName = "jsevaluator"
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFileName.endsWith('.aar')) {
                outputFileName = "${archivesBaseName}-${version}.aar"
            }
        }
    }
    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.4.0'
}

// To publish, run ./gradlew :jsevaluator:publishAllPublicationsToMyrepoRepository
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'com.github.singularity-group'
                artifactId 'js-evaluator-for-android'
                version = android.defaultConfig.versionName
            }
        }
        repositories {
            repositories {
                maven {
                    name = 'myrepo'
                    url = "${project.buildDir}/repo"
                }
            }
        }
    }
}
